global *

-- error handling
local clean = true
err = (msg, lvl = 2) -> 
	if clean
		error msg, lvl
		clean = false
	
-- index symbols
kind = {'kind'}

-- type guard helpers
local primitive =
	num: 'number', bol: 'boolean', str: 'string',
	fun: 'function', tbl: 'table', udt: 'userdata'

is =
	<call>: (q, x, y = true, n = false) =>
		p, t = primitive[q], type x
		res = t == p or (t == 'table' and q == x[kind])
		res and y or n
	<index>: (q1, s = @) => {
		<call>: (x, y, n) => s q1, x, y, n
		<index>: (q2) => (x1, x2, y, n) ->
			s(q1, x1) and s(q2, x2, y, n)
	}

-- iterators for specific key types
kpair = (tg) -> (t, k) ->
	k, v = next(t, k)
	k, v = next(t, k) while v? and not tg k
	k, v

spairs = (t) -> kpair(is.str), t
wpairs = (t) -> kpair(is.wrd), t

-- string iterators
local str = ''.<index>
str.words = => @\gmatch('%S+')

-- augmented bare words
local word
external = ->
	src = debug.getinfo(3, 'S').source
	path = '@' .. vim.fn.stdpath'config' .. '/pack/'
	src\find(path, 1, true) == 1

bare = true
_G.<index> = (n) =>
	return nil if not bare
	word.create n

word =
	create: (n, k = 'wrd') -> 
		<>: word.<>, [kind]: k, :n, v: true
	<tostring>: => tostring(@n)
	<call>: (w = '') => @n .. ' ' .. (w.n or w)
	<unm>: =>
		@v = false
		@

-- branded words
brand = (b) -> <index>: (n) => word.create n, b.n or b
